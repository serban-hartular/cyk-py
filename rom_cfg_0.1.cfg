%%alias Case,Gender,Number CGN 
%%alias Case,Gender,Number,lemma CGNL 
%%alias lemma L

DetP ::= h:DET
DetP ::= h:QP
DetP[CGN=@] ::= DET[CGN=@] QP[CGN=@]
DetP ::= ADV[lemma=nici] h:DET[lemma=un]

QP ::= h:NUM
QP ::= h:NUM ADP[lemma=de]

AdjP ::= h:ADJ
AdjP ::= AdvSpec h:ADJ
AdjP ::= DET[lemma=cel CGN=@] h:AdjP[CGN=@]
AdjP[Number=@ L=@] ::= RP[Number=@ L=care,ce]
AdjP[CGNL=@] ::= VP[CGNL=@ VerbForm==Part]
AdjP ::= h:AdjP PP

N0[Person=3] ::= h:NOUN
N0[det=T Person=3 CGN=@] ::= det:DetP[CGN=@] h:N0[CGN=@ det=F Definite=Ind]  # un om; *un omul
N0[CGN=@]       ::= AdjP[CGN=@ Definite=Ind] h:N0[CGN=@ det=F]        # frumos baiat
N0[CGN=@ det=T] ::= AdjP[CGN=@ Definite=Def] h:N0[CGN=@ det=F]        # frumosul baiat
N0 ::= h:N0 id:QP[CGN=@]                                    # etajul doi


NP ::= h:N0
NP ::= h:PRON[PronType=Prs,Ind,Dem,Neg Strength=Strong]     # eu, acesta, cineva, nimeni
NP[Person=3] ::= h:PROPN                                    # Ion
NP[CGN=@] ::= NP[Definite=Def CGN=@] DET[PronType=Dem lemma=acesta,acela]
NP[CGN=@] ::= h:NP[CGN=@] AdjP[CGN=@]
NP ::= h:NP poss:NP[Case==Gen]
POSS[PosG=@1 PosN=@2] ::= DET[lemma=al Gender=@1 Number=@2] poss:NP[Case==Gen]
NP ::= h:NP[Gender=@1 Number=@2] POSS[PosG=@1 PosN=@2]
NP ::= h:NP PP
NP ::= h:NP appos:NP[Case=Nom]

AdvSpec ::= h:ADV[lemma=prea,foarte,tare,mai]
AdvSpec ::= ADV[lemma=mult,și] h:ADV[lemma=mai]
AdvSpec ::= DET[lemma=cel] h:ADV[lemma=mai]
AdvSpec ::= ADV ADP[lemma=de]    # teribil de 
AdvP ::= h:ADV
AdvP ::= AdvSpec h:ADV
AdvP ::= Prep h:AdvP # pe sus, de departe
AdvP ::= h:AdvP PP

Prep ::= h:ADP # Prepozitie
Prep[Case=@] ::= Prep ADP[Case=@] # de la, pe la, de pe la, 
Prep[Case=Acc] ::= CA_SI # ca și mine
PP ::= h:Prep[Case=@ Case=Acc,Gen,Dat] NP[Case=@]
PP ::= h:Prep VP[VerbForm=Inf]    # pentru a scapa
PP ::= h:ADV[lemma=ca,precum] NP

%%alias VerbForm,Mood,Tense,Person,Number,lemma VMTPNL
%%alias Mood,Tense,Person,Number,lemma MTPNL
%%alias Person,Number,lemma PNL
%%alias Person,Number PN
%%alias Number,lemma NL

V0 ::= h:VERB[VerbForm=Fin,Ger,Part]
#infinitivul, supinul
V0 ::= PART[PartType==Inf] h:VERB[VerbForm=Inf]
V0[VerbForm=Supin lemma=@] ::= ADP[lemma=de,la,pentru,spre,după] VERB[VerbForm=Part Gender=Masc Number=Sing lemma=@]
#a putea modal
V0[lemma=@1] ::= h:V0[lemma==putea] VERB[VerbForm=Inf lemma=@1] 

AUXP ::= h:AUX
AUXP ::= h:AUXP ADV        # am tot cam mai venit
# perfectul compus
V0[VerbForm=Fin Mood=Ind Tense=Perf Comp=T PNL=@] ::= AUXP[lemma=avea PN=@] VERB[VerbForm=Part Gender=Masc Number=Sing lemma=@] 
#conditionalul
V0[VerbForm=Fin Mood=Cond Tense=Pres Comp=T PN=@] ::= AUXP[lemma=avea PN=@] h:VERB[VerbForm=Inf]
FI_PART[lemma=@] ::= AUX[lemma=fi Tense=Pres VerbForm=Inf] VERB[VerbForm=Part Gender=Masc Number=Sing lemma=@]
V0[VerbForm=Fin Mood=Cond Tense=Perf PN=@ Comp=T lemma=@] ::= AUXP[lemma=avea PN=@] FI_PART[lemma=@]
#subjonctivul
SPART ::= h:PART[Mood=Sub lemma=să]
SPART[neg=T] ::= h:SPART[neg=F] PART[Polarity==Neg] # să nu vină
SPART ::= h:SPART ADV # să nu mai tot vină
V0[VerbForm=Fin Mood=Sub Tense=Pres] ::= SPART h:V0[VerbForm=Fin Mood=Sub Tense=Pres Pers=3]
V0[VerbForm=Fin Mood=Sub Tense=Pres] ::= SPART h:V0[VerbForm=Fin Mood=Sub,Ind Tense=Pres Pers=1,2]
V0[VerbForm=Fin Mood=Sub Tense=Perf lemma=@] ::= SPART FI_PART[lemma=@]

DCLT ::= h:PRON[PronType=Prs Strength=Weak Case=Acc]
ICLT ::= h:PRON[PronType=Prs Strength=Weak Case=Dat]
# simple tenses
V0[Dclt=T DirPers=@1 DirNum=@2 DirGen=@3] ::= dclt:DCLT[Person=@1 Number=@2 Gender=@3] h:V0[Dclt=F Iclt=F VerbForm=Fin Comp=F]
V0[Iclt=T IndirPers=@1 IndirNum=@2 IndirGen=@3] ::= iclt:ICLT[Person=@1 Number=@2 Gender=@3] h:V0[Iclt=F VerbForm=Fin Comp=F]
# perfect composite
V0[Dclt=T DirPers=@1 DirNum=@2 DirGen=Masc] ::= dclt:DCLT[Person=@1 Number=@2 Gender=Masc] h:V0[VerbForm=Fin Comp==T Dclt=F Iclt=F]
V0[Dclt=T DirPers=@1 DirNum=@2 DirGen=Fem] ::= h:V0[VerbForm=Fin Comp==T Dclt=F] dclt:DCLT[Person=@1 Number=@2 Gender=Fem]
V0[Iclt=T IndirPers=@1 IndirNum=@2 IndirGen=@3] ::= iclt:ICLT[Person=@1 Number=@2 Gender=@3] h:V0[VerbForm=Fin Comp==T Iclt=F]

PE_DOBJ ::= ADP[lemma=pe] h:NP[Case=Acc]

VP ::= h:V0
VP[neg=T] ::= PART[Polarity==Neg] h:V0
# subject
VP[subj=T SubjG=@1] ::= subj:NP[Case=Nom PN=@ Gender=@1] h:VP[VerbForm=Fin subj=F PN=@]
%reverse
# direct object
VP[dobj=T] ::= h:VP[dobj=F VerbForm=Fin,Ger,Inf DirPers=@1 DirNum=@2 DirGen=@3] dobj:NP[Case=Acc Person=@1 Number=@2 Gender=@3]
%reverse
VP[dobj=T] ::= h:VP[dobj=F Dclt==T VerbForm=Fin,Ger,Inf DirPers=@1 DirNum=@2 DirGen=@3] dobj:PE_DOBJ[Person=@1 Number=@2 Gender=@3]
%reverse
VP[dobj=T] ::= h:VP[dobj=F VerbForm=Fin,Ger,Inf] dobj:VP[Mood==Sub]
# indirect object
VP[iobj=T] ::= h:VP[iobj=F IndirPers=@1 IndirNum=@2 IndirGen=@3] iobj:NP[Case==Dat Person=@1 Number=@2 Gender=@3]
%reverse
# copulative predicate
VP[cop=T] ::= h:VP[cop=F dobj=F SubjG=@1 Number=@ lemma=fi] npred:NP[Case=Nom Gender=@1 Number=@]  
VP[cop=T] ::= h:VP[cop=F dobj=F SubjG=@1 Number=@ lemma=fi] npred:AdjP[Case=Nom Gender=@1 Number=@]  

# adverbs, Prepositional phrases, complement phrases, gerunds
VP ::= h:VP AdvP
%reverse
VP ::= h:VP PP
%reverse
VP ::= h:VP CP
%reverse
VP ::= h:VP VP[VerbForm=Ger]
%reverse

CP ::= SConj VP[VerbForm=Fin]
REL ::= h:PRON[PronType==Rel]
REL ::= h:ADV[PronType==Rel]
REL ::= Prep[Case=@] REL[Case=@] # de când, în care, etc.
RP ::= h:REL VP

VP ::= h:VP CP
%reverse
VP ::= h:VP RP
%reverse

# coordination
%%alias VerbForm,Mood VM
VPConj ::= h:VP CCONJ
VP[VM=@] ::= VPConj[VM=@] h:VP[VM=@]

NPConj ::= h:NP CCONJ # durata și
NP[Number=Plur] ::= h:NPConj NP  # durata și mortalitatea
NPConj ::= NP h:NPConj # sanatatea, durata și

AdjPConj ::= h:AdjP CCONJ  
AdjP[CGN=@] ::= h:AdjPConj[CGN=@] AdjP[CGN=@] 

AdvPConj ::= h:AdvP CCONJ  
AdvP ::= h:AdvPConj AdvP 

PPConj ::= PP CCONJ 
PP ::= h:PPConj PP 

SConj ::= SCONJ
SConj ::= ADV[lemma=ca]
SConj ::= ADV SCONJ	# cum că, chiar dacă, chiar de
SConj ::= ADP SCONJ # pentru că
BARE_PP ::= ADP NOUN[Definite=Ind] # pe lângă
BARE_PP ::= ADP PRON[PronType=Ind,Rel] # cu toate, după ce
SConj ::= BARE_PP SCONJ # pe lângă că, cu toate că
SConj ::= BARE_PP PRON[PronType=Rel] # pe măsură ce
SConj ::= DET[lemma=cât CGN=@] NOUN[CGN=@ Case=Nom,Acc]  # câtă vreme, cât timp
SConj ::= ADP PRON[lemma=ce] # după ce, până ce
SConj ::= ADP[lemma=după] PRON[lemma=care] # după ce, până ce
SConj ::= ADV PRON[lemma=ce] # îndată ce, odată ce, 
SConj ::= ADP ADV[PronType=Rel lemma=cum,când,cât]	# după cum, pe când, după cât
CA_SI ::= ADV[lemma=ca] CCONJ[lemma=și]
SConj ::= CA_SI ADV[PronType=Rel lemma=când,cum] # ca și când, ca și cum
# nu stiu ce să fac cu: în concluzie, în consecință, ca atare
