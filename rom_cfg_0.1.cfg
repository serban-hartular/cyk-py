%%alias Case,Gender,Number CGN 
%%alias Case,Gender,Number,lemma CGNL 
%%alias lemma L

DetP ::= h:DET
DetP ::= h:QP
DetP[CGN=@] ::= h:DET[CGN=@] h:QP[CGN=@]
DetP ::= ADV[lemma=nici] h:DET[lemma=un]

QP ::= h:NUM

AdjP ::= h:ADJ
AdjP ::= AdvMod h:ADJ
AdjP ::= DET[lemma=cel CGN=@] h:AdjP[CGN=@]
AdjP[Number=@ L=@] ::= RP[Number=@ L=care,ce]
AdjP[CGNL=@] ::= VP[CGNL=@ VerbForm==Part]
AdjP[CGN=@ L=@] ::= AdjP[CGNL=@] AdjPConj[CGN=@]
AdjPConj ::= CCONJ h:AdjP[CGN=@] 

N0[Person=3] ::= h:NOUN
N0[det=T Person=3 CGN=@] ::= det:DetP[CGN=@] h:N0[CGN=@ det=F Definite=Ind]  # un om; *un omul
N0[CGN=@]       ::= AdjP[CGN=@ Definite=Ind] h:N0[CGN=@ det=F]        # frumos baiat
N0[CGN=@ det=T] ::= AdjP[CGN=@ Definite=Def] h:N0[CGN=@ det=F]        # frumosul baiat
N0 ::= h:N0 id:QP[CGN=@]                                    # etajul doi


NP ::= h:N0
NP ::= h:PRON[PronType=Prs,Ind,Dem,Neg Strength=Strong]     # eu, acesta, cineva, nimeni
NP[Person=3] ::= h:PROPN                                    # Ion
NP[CGN=@] ::= NP[Definite=Def CGN=@] DET[PronType=Dem lemma=acesta,acela]
NP[CGN=@] ::= h:NP[CGN=@] AdjP[CGN=@]
NP ::= h:NP poss:NP[Case==Gen]
POSS[PosG=@1 PosN=@2] ::= DET[lemma=al Gender=@1 Number=@2] poss:NP[Case==Gen]
NP ::= h:NP[Gender=@1 Number=@2] POSS[PosG=@1 PosN=@2]
NP ::= h:NP PP
NP ::= h:NP appos:NP[Case=Nom]

AdvMod ::= h:ADV[lemma=prea,foarte,tare,mai]
AdvMod ::= ADV[lemma=mult,și] h:ADV[lemma=mai]
AdvMod ::= DET[lemma=cel] h:ADV[lemma=mai]
AdvMod ::= ADV ADP[lemma=de]    # teribil de 
AdvP ::= h:ADV
AdvP ::= AdvMod h:ADV
AdvP ::= PREP h:AdvP # pe sus, de departe

PREP ::= h:ADP
PREP[Case=@] ::= PREP ADP[Case=@]
PP ::= h:PREP[Case=@ Case=Acc,Gen,Dat] NP[Case=@]
PP ::= h:PREP VP[VerbForm=Inf]    # pentru a scapa
PP ::= h:ADV[lemma=ca,precum] NP

%%alias VerbForm,Mood,Tense,Person,Number,lemma VMTPNL
%%alias Mood,Tense,Person,Number,lemma MTPNL
%%alias Person,Number,lemma PNL
%%alias Person,Number PN
%%alias Number,lemma NL

V0 ::= h:VERB[VerbForm=Fin,Ger,Part]
#infinitivul, supinul
V0 ::= PART[PartType=Inf] h:VERB[VerbForm=Inf]
V0[VerbForm=Supin lemma=@] ::= ADP[lemma=de,la,pentru,spre,după] VERB[VerbForm=Part Gender=Masc Number=Sing lemma=@]
#a putea modal
V0[lemma=@1] ::= h:V0[lemma==putea] VERB[VerbForm=Inf lemma=@1] 

AUXP ::= h:AUX
AUXP ::= h:AUXP ADV        # am tot cam mai venit
# perfectul compus
V0[VerbForm=Fin Mood=Ind Tense=Perf Comp=T PNL=@] ::= AUXP[lemma=avea PN=@] VERB[VerbForm=Part Gender=Masc Number=Sing lemma=@] 
#conditionalul
V0[VerbForm=Fin Mood=Cond Tense=Pres Comp=T PN=@] ::= AUXP[lemma=avea PN=@] h:VERB[VerbForm=Inf]
FI_PART[lemma=@] ::= AUX[lemma=fi Tense=Pres VerbForm=Inf] VERB[VerbForm=Part Gender=Masc Number=Sing lemma=@]
V0[VerbForm=Fin Mood=Cond Tense=Perf PN=@ Comp=T lemma=@] ::= AUXP[lemma=avea PN=@] FI_PART[lemma=@]
#subjonctivul
SPART ::= h:PART[Mood=Sub lemma=să]
SPART ::= h:SPART ADV # să tot vină
V0[VerbForm=Fin Mood=Sub Tense=Pres] ::= SPART h:V0[VerbForm=Fin Mood=Sub Tense=Pres Pers=3]
V0[VerbForm=Fin Mood=Sub Tense=Pres] ::= SPART h:V0[VerbForm=Fin Mood=Sub,Ind Tense=Pres Pers=1,2]
V0[VerbForm=Fin Mood=Sub Tense=Perf lemma=@] ::= SPART FI_PART[lemma=@]

DCLT ::= h:PRON[PronType=Prs Strength=Weak Case=Acc]
ICLT ::= h:PRON[PronType=Prs Strength=Weak Case=Dat]
# simple tenses
V0[Dclt=T DcltP=@1 DcltN=@2 DcltG=@3] ::= dclt:DCLT[Person=@1 Number=@2 Gender=@3] h:V0[Dclt=F Iclt=F VerbForm=Fin Comp=F]
V0[Iclt=T IcltP=@1 IcltN=@2 IcltG=@3] ::= iclt:ICLT[Person=@1 Number=@2 Gender=@3] h:V0[Iclt=F VerbForm=Fin Comp=F]
# perfect composite
V0[Dclt=T DcltP=@1 DcltN=@2 DcltG=Masc] ::= dclt:DCLT[Person=@1 Number=@2 Gender=Masc] h:V0[VerbForm=Fin Comp==T Dclt=F Iclt=F]
V0[Dclt=T DcltP=@1 DcltN=@2 DcltG=Fem] ::= h:V0[VerbForm=Fin Comp==T Dclt=F] dclt:DCLT[Person=@1 Number=@2 Gender=Fem]
V0[Iclt=T IcltP=@1 IcltN=@2 IcltG=@3] ::= iclt:ICLT[Person=@1 Number=@2 Gender=@3] h:V0[VerbForm=Fin Comp==T Iclt=F]

PE_DOBJ ::= ADP[lemma=pe] h:NP[Case=Acc]

VP ::= h:V0
VP[neg=T] ::= PART[Polarity==Neg] h:V0
# subject
VP[subj=T SubjG=@1] ::= subj:NP[Case=Nom PN=@ Gender=@1] h:VP[VerbForm=Fin subj=F PN=@]
%reverse
# direct object
VP[dobj=T] ::= h:VP[dobj=F VerbForm=Fin,Ger,Inf DcltP=@1 DcltN=@2 DcltG=@3] dobj:NP[Case=Acc Person=@1 Number=@2 Gender=@3]
%reverse
VP[dobj=T] ::= h:VP[dobj=F Dclt==T VerbForm=Fin,Ger,Inf DcltP=@1 DcltN=@2 DcltG=@3] dobj:PE_DOBJ[Person=@1 Number=@2 Gender=@3]
%reverse
VP[dobj=T] ::= h:VP[dobj=F VerbForm=Fin,Ger,Inf] dobj:VP[Mood==Sub]
# indirect object
VP[iobj=T] ::= h:VP[iobj=F IcltP=@1 IcltN=@2 IcltG=@3] iobj:NP[Case==Dat Person=@1 Number=@2 Gender=@3]
%reverse
# copulative predicate
VP[cop=T] ::= h:VP[cop=F dobj=F SubjG=@1 Number=@ lemma=fi] npred:NP[Case=Nom Gender=@1 Number=@]  
VP[cop=T] ::= h:VP[cop=F dobj=F SubjG=@1 Number=@ lemma=fi] npred:AdjP[Case=Nom Gender=@1 Number=@]  

# adverbs, prepositional phrases
VP ::= h:VP AdvP
%reverse
VP ::= h:VP PP
%reverse

SConj ::= SCONJ
SConj ::= ADP[lemma=pentru] ADV[lemma=ca]
CP ::= SConj VP[VerbForm=Fin]
REL ::= PRON[PronType==Rel]
REL ::= ADV[PronType==Rel]
REL ::= PREP[Case=@] REL[Case=@]
RP ::= REL VP

VP ::= h:VP CP
%reverse
VP ::= h:VP RP
%reverse

