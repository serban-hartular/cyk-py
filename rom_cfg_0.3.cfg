%%alias Case,Gender,Number CGN 
%%alias Case,Gender,Number,lemma CGNL 
%%alias Gender,Number GN 

%%alias lemma L

# TODO remove complet=F/T

%%alias VerbForm,Mood VM

NP ::= CCONJ[lemma=și,nici] h:NP
DetP ::= CCONJ[lemma=și,nici] h:DetP
NP ::= AdvClit h:NP
DetP ::= AdvClit h:DetP
QP ::= AdvP h:QP


DetP ::= h:DET
DetP ::= h:QP
DetP[CGN=@] ::= DET[CGN=@] QP[CGN=@]
DetP ::= ADV[lemma=nici] h:DET[lemma=un]
DetP ::= AdvSpec h:DetP  # aproape toți (copiii)

QP ::= h:NUM
QP ::= DET[Gender=@ Case=@] NUM[Gender=@ Case=@ lemma=sută,mie,milion,miliard,bilion]
QP[Number=Plur] ::= h:QP NUM
QP[Gender=Masc,Fem] ::= h:QP ADP[lemma=de] # mii de ani -- nu se acorda
QP[GN=@] ::= DET[lemma=al GN=@] h:NUM[GN=@ NumType=Ord] # a patra 

AdjP ::= h:ADJ
AdjP ::= AdvSpec h:AdjP
AdjP ::= DET[lemma=cel CGN=@] h:AdjP[CGN=@]
#AdjP[Number=@ L=@] ::= RP[Number=@ L=care,ce]
AdjP[CGNL=@] ::= VP[CGNL=@ VerbForm==Part]
AdjP ::= h:AdjP PP
%reverse # aschii de mii de ani pribege
AdjP ::= h:AdjP AdvP
%reverse
AdjP ::= h:AdjP iobj:NP[Case==Dat] # state riverane Dunării
%reverse

N0[Person=3 bare=T] ::= h:NOUN
N0[Person=3 bare=T Definite=@ CGN=@] ::= h:NOUN[CGN=@] DET[PronType=Art Variant=Short CGN=@ Definite=@] # pH-ul 
N0[det=T Person=3 CGN=@ bare=F] ::= det:DetP[CGN=@] h:N0[CGN=@ det=F Definite=Ind]  # un om; *un omul
N0[det=T Person=3 CGN=@ bare=F] ::= det:DetP[CGN=@ PronType=Emp] h:N0[CGN=@ det=F Definite=Def]  # însăși natura lucrurilor
N0[det=T Person=3 CGN=@ bare=F] ::= det:DetP[CGN=@ PronType=Ind lemma=tot] h:N0[CGN=@ det=F Definite=Def]  # ??
N0 ::= AdvSpec[lemma=astfel,altfel bare=F] h:N0 # un altfel de țipăt
N0[CGN=@ bare=F]       ::= AdjP[CGN=@ Definite=Ind] h:N0[CGN=@ det=F Definite=Ind]        # frumos baiat
N0[CGN=@ det=T bare=F] ::= AdjP[CGN=@ Definite=Def] h:N0[CGN=@ det=F Definite=Ind]        # frumosul baiat
N0 ::= h:N0 id:QP[CGN=@ bare=F]                                    # etajul doi

NP[PronPers=T] ::= h:PRON[PronType=Prs Strength=Strong Poss=No]     # eu, tu, etc
NP ::= h:PRON[PronType=Ind,Dem,Neg] # acesta, cineva, nimeni

NP ::= h:N0
NP[CGN=@ bare=F] ::= NP[Definite=Def CGN=@] DET[PronType=Dem lemma=acesta,acela CGN=@]
NP[CGN=@ bare=F] ::= NP[Definite=Def CGN=@] DET[PronType=Emp CGN=@] # Mircea însuși
NP[CGN=@ bare=F] ::= h:NP[CGN=@] AdjP[CGN=@] # om frumos
NP[bare=F] ::= h:NP poss:NP[Case==Gen] # omul lui cutare
NP[bare=F] ::= h:NP poss:PRON[PronType==Prs Poss==Yes] # inmormantarea sa
NP[bare=F GN=@] ::= h:NP[GN=@] poss:DET[PronType==Prs Poss==Yes lemma=meu,tău,lui,său GN=@] # inmormantarea sa
POSS[PosG=@1 PosN=@2] ::= DET[lemma=al Gender=@1 Number=@2] h:NP[Case==Gen] # al Mariei
POSS[PosG=@1 PosN=@2] ::= DET[lemma=al Gender=@1 Number=@2] h:PRON[PronType==Prs Poss==Yes Gender=@1 Number=@2] # al său
POSS[PosG=@1 PosN=@2] ::= fixed:DET[lemma=al Gender=@1 Number=@2] h:DET[PronType==Prs Poss==Yes Gender=@1 Number=@2] # al meu
NP[bare=F] ::= h:NP[Gender=@1 Number=@2] poss:POSS[PosG=@1 PosN=@2] # om al Mariei, omul verde al Mariei
NP[bare=F] ::= h:NP PP # om de fier
NP[bare=F] ::= h:NP[Number=@] RP[Number=@ L=care,ce]   # omul care vin, oamenii cărora li s-au furat banii
NP[bare=F] ::= h:NP appos:NP[Case=Nom]
NP ::= h:NP CP # faptul că cutare este cumva 

N0[Person=3] ::= h:PropN                                    # Ion
PropN ::= h:PROPN
PropN ::= h:PropN PROPN

AdvClit ::= h:ADV[lemma=și,nici,chiar,doar,numai,tot]

AdvSpec ::= h:ADV[lemma=prea,foarte,tare,mai,aproape]
AdvSpec ::= h:CCONJ[lemma=și,nici]
AdvSpec ::= ADV[lemma=mult] h:ADV[lemma=mai]
AdvSpec ::= DET[lemma=cel] h:ADV[lemma=mai]
AdvSpec ::= h:ADV ADP[lemma=de]    # teribil de 
AdvSpec ::= ADV[lemma=chiar] h:AdvSpec
AdvP ::= h:ADV[Full=Yes]
AdvP ::= AdvSpec h:ADV
AdvP ::= Prep h:AdvP # pe sus, de departe
AdvP ::= h:AdvP PP

AdvP ::= h:CCONJ[lemma=însă,deci]

AdjComp ::= AdvSpec[lemma=mai] h:ADJ
AdvComp ::= AdvSpec[lemma=mai] h:ADV
Comp2 ::= ADV[lemma=ca,decât] h:NP # mai frumos ca el
Comp2 ::= ADV[lemma=ca,decât] h:ADV # mai frumos ca acum
AdvP ::= h:AdvComp Comp2 
AdjP ::= h:AdjComp Comp2 

Prep ::= h:ADP # Prepozitie
Prep[Case=@] ::= Prep ADP[Case=@] # de la, pe la, de pe la, 
Prep[Case=Acc] ::= CA_SI # ca și mine
PP ::= h:Prep[Case=@ Case=Acc,Gen,Dat] NP[Case=@]
PP ::= h:Prep VP[VerbForm=Inf]    # pentru a scapa
PP ::= h:ADV[lemma=ca,precum] NP
PP ::= Prep h:ADV[Full=Yes]	# de azi, de ieri, de mâine 
PP ::= AdvP h:PP # aparent fara motiv
PP ::= AdvSpec h:PP # mai pe urma

%%alias VerbForm,Mood,Tense,Person,Number,lemma VMTPNL
%%alias VerbForm,Mood,Aspect,Tense,Person,Number VMATPN
%%alias Mood,Tense,Person,Number,lemma MTPNL
%%alias Person,Number,lemma PNL
%%alias Person,Number PN
%%alias Number,lemma NL

AdvInt ::= h:ADV[lemma=mai,tot,cam]

V0 ::= h:VERB[VerbForm=Fin,Ger,Part,Inf]
#infinitivul, supinul
V0 ::= PART[PartType==Inf] h:V0[VerbForm=Inf]
V0[VerbForm=Supin lemma=@] ::= ADP[lemma=de,la,pentru,spre,după] VERB[VerbForm=Part Gender=Masc Number=Sing lemma=@]
#a putea modal
V0[lemma=@1 VMATPN=@] ::= mod:V0[lemma==putea VMATPN=@] h:VERB[VerbForm=Inf] 

AUXP ::= h:AUX
AUXP ::= h:AUXP AdvInt        # am tot cam mai venit
# perfectul compus
V0[VerbForm=Fin Mood=Ind Tense=Past Asp=Perf Compound=T PNL=@] ::= AUXP[lemma=avea PN=@] VERB[VerbForm=Part Gender=Masc Number=Sing lemma=@] 
#conditionalul
V0[VerbForm=Fin Mood=Cond Tense=Pres Compound=T PN=@] ::= AUXP[lemma=avea PN=@ Mood=Cond] h:VERB[VerbForm=Inf]
FI_PART[lemma=@] ::= AUX[lemma=fi Tense=Pres VerbForm=Inf] VERB[VerbForm=Part Gender=Masc Number=Sing lemma=@]
V0[VerbForm=Fin Mood=Cond Tense=Past Asp=Perf PN=@ Compound=T lemma=@] ::= AUXP[lemma=avea PN=@] FI_PART[lemma=@]
# viitorul
V0[VerbForm=Fin Mood=Ind Tense=Fut Compound=T PN=@] ::= AUXP[lemma=vrea PN=@] h:VERB[VerbForm=Inf]
V0[VerbForm=Fin Mood=Ind Tense=Fut Asp=Perf PN=@ Compound=T lemma=@] ::= AUXP[lemma=vrea PN=@] FI_PART[lemma=@]
#subjonctivul
SPART ::= h:PART[Mood=Sub lemma=să]
SPART[neg=T] ::= h:SPART[neg=F] PART[Polarity==Neg] # să nu vină
SPART ::= h:SPART AdvInt # să nu mai tot vină
V0[VerbForm=Fin Mood=Sub Tense=Pres] ::= SPART h:V0[VerbForm=Fin Mood=Sub Tense=Pres Person=3]
V0[VerbForm=Fin Mood=Sub Tense=Pres] ::= SPART h:V0[VerbForm=Fin Mood=Sub,Ind Tense=Pres Person=1,2]
V0[VerbForm=Fin Mood=Sub Tense=Past Asp=Perf lemma=@] ::= SPART FI_PART[lemma=@]

V0 ::= AdvInt h:V0[Compound=F] # te mai văd
# pronume clitice
DCLT ::= h:PRON[PronType=Prs Strength=Weak Case=Acc]
ICLT ::= h:PRON[PronType=Prs Strength=Weak Case=Dat]
# simple tenses, infinitive
V0[Dclt=T DirPers=@1 DirNum=@2 DirGen=@3 Reflex=@] ::= dclt:DCLT[Person=@1 Number=@2 Gender=@3 Reflex=@] h:V0[Dclt=F Iclt=F VerbForm=Fin,Inf Compound=F]
V0[Iclt=T IndirPers=@1 IndirNum=@2 IndirGen=@3] ::= iclt:ICLT[Person=@1 Number=@2 Gender=@3] h:V0[Iclt=F VerbForm=Fin,Inf Compound=F]

# perfect composite
V0[Dclt=T DirPers=@1 DirNum=@2 DirGen=Masc Reflex=@] ::= dclt:DCLT[Person=@1 Number=@2 Gender=Masc Reflex=@] h:V0[VerbForm=Fin Compound==T Dclt=F Iclt=F]
V0[Dclt=T DirPers=@1 DirNum=@2 DirGen=Fem Reflex=@] ::= h:V0[VerbForm=Fin Compound==T Dclt=F Reflex=@] dclt:DCLT[Person=@1 Number=@2 Gender=Fem]
V0[Iclt=T IndirPers=@1 IndirNum=@2 IndirGen=@3] ::= iclt:ICLT[Person=@1 Number=@2 Gender=@3] h:V0[VerbForm=Fin Compound==T Iclt=F]
# imperative, gerund
V0[Dclt=T DirPers=@1 DirNum=@2 DirGen=@3 Reflex=@] ::= h:V0[Mood=Imp Dclt=F Iclt=F VerbForm=Fin Compound=F Reflex=@] dclt:DCLT[Person=@1 Number=@2 Gender=@3]
V0[Iclt=T IndirPers=@1 IndirNum=@2 IndirGen=@3] ::= h:V0[ Mood=Imp Iclt=F VerbForm=Fin Compound=F] iclt:ICLT[Person=@1 Number=@2 Gender=@3]
V0[Dclt=T DirPers=@1 DirNum=@2 DirGen=@3 Reflex=@] ::= h:V0[VerbForm=Ger Dclt=F Iclt=F Reflex=@] dclt:DCLT[Person=@1 Number=@2 Gender=@3]
V0[Iclt=T IndirPers=@1 IndirNum=@2 IndirGen=@3] ::= h:V0[VerbForm=Ger Iclt=F] iclt:ICLT[Person=@1 Number=@2 Gender=@3]

# locutiuni
# V0 ::= h:V0[lemma=avea] locut:NP[lemma=loc,chef,voie] 

PE_DOBJ ::= ADP[lemma=pe] h:NP[Case=Acc]

VP ::= h:V0
VP[neg=T] ::= PART[Polarity==Neg] h:V0
# subject
VP[subj=T SubjG=@1] ::= subj:NP[Case=Nom PN=@ Gender=@1] h:VP[VerbForm=Fin subj=F SubjG=@1 PN=@ complet=F]
%reverse
VP[subj=T] ::= h:VP[VerbForm=Fin subj=F Person=3 Number=Sing complet=F] subj:VP[Mood==Sub]
%reverse
VP[subj=T] ::= h:VP[VerbForm=Fin subj=F Person=3 Number=Sing complet=F] subj:VP[VerbForm=Inf]
%reverse 
# direct object
VP[dobj=T] ::= h:VP[dobj=F cop=F VerbForm=Fin,Ger,Inf DirPers=@1 DirNum=@2 DirGen=@3 complet=F Reflex=No] dobj:NP[PronPers=F Case=Acc Person=@1 Number=@2 Gender=@3]
%reverse
VP[dobj=T] ::= h:VP[dobj=F cop=F Dclt==T VerbForm=Fin,Ger,Inf DirPers=@1 DirNum=@2 DirGen=@3 complet=F Reflex=No] dobj:PE_DOBJ[Person=@1 Number=@2 Gender=@3]
%reverse
VP[dobj=T] ::= h:VP[dobj=F cop=F VerbForm=Fin,Ger,Inf complet=F Reflex=No] dobj:VP[Mood==Sub]
VP[dobj=T] ::= h:VP[dobj=F cop=F VerbForm=Fin,Ger,Inf complet=F Reflex=No] dobj:VP[VerbForm=Inf]
VP[dobj=T] ::= h:VP[dobj=F cop=F VerbForm=Fin,Ger,Inf complet=F Reflex=Yes] dobj:VP[VerbForm=Inf] # inherently reflexive
# indirect object
VP[iobj=T] ::= h:VP[iobj=F IndirPers=@1 IndirNum=@2 IndirGen=@3 complet=F] iobj:NP[Case==Dat Person=@1 Number=@2 Gender=@3]
%reverse
# secondary object
VP[obj2=T] ::= h:VP[obj2=F dobj==T] obj2:NP[Case=Nom,Acc]
%reverse
VP[obj2=T] ::= h:VP[obj2=F dobj==T] obj2:VP[Mood==Sub]
%reverse
VP[obj2=T] ::= h:VP[obj2=F pass==T] obj2:NP[Case=Nom,Acc]
%reverse  # Ion a fost invatat/imputernicit sa faca cutare  
VP[obj2=T] ::= h:VP[obj2=F pass==T] obj2:VP[Mood==Sub]
%reverse
# copulative predicate
VP[cop=T Number=@] ::= h:VP[cop=F pass=F Number=@ lemma=fi complet=F] npred:NP[Case=Nom Number=@] # here we dont enforce gender agreement - suprafata corpului negru este un bun etalon  
VP[cop=T SubjG=@1 Number=@] ::= h:VP[cop=F pass=F SubjG=@1 Number=@ lemma=fi complet=F] npred:AdjP[Case=Nom Gender=@1 Number=@]
VP[cop=T] ::= h:VP[cop=F pass=F lemma=fi complet=F] npred:PP
VP[cop=T] ::= h:VP[cop=F pass=F lemma=fi complet=F] npred:VP[VerbForm=Supin]
# passive voice
VP[pass=T lemma=@ SubjG=@1 Number=@] ::= h:VP[pass=F cop=F SubjG=@1 Number=@ lemma=fi complet=F] pass:VP[VerbForm=Part Gender=@1 Number=@ lemma=@]
# predicativ suplimentar
VP[predsup=T SubjG=@1 Number=@] ::= h:VP[predsup=F cop=F SubjG=@1 Number=@ VerbForm=Fin,Ger complet=F] predsup:AdjP[Case=Nom Gender=@1 Number=@]
%reverse
VP[cpredobj=T SubjG=@1 Number=@] ::= h:VP[cpredobj=F cop=F dobj=F SubjG=@1 Number=@ complet=F] cpredobj:NP[Case=Nom Gender=@1 Number=@ PronPers=F]
%reverse # se socotea un bard al natiunii
VP[cpredobj=T SubjG=@1 Number=@] ::= h:VP[cpredobj=F cop=F dobj=F SubjG=@1 Number=@ complet=F] cpredobj:PP[lemma=ca] # notat ca dangereux
%reverse
# nominal circumstantials
VP ::= h:VP ntmod:NP[Case=Nom]
%reverse

# adverbs, Prepositional phrases, complement phrases, gerunds
VP ::= h:VP RP
%reverse
VP ::= h:VP[complet=F] AdvP
%reverse
VP ::= h:VP[complet=F] PP
%reverse
VP[complet=T] ::= h:VP[complet=F] CP
%reverse
VP ::= h:VP[complet=F] advcl:VP[VerbForm==Ger]
%reverse
VP[cop=T] ::= h:VP[cop=F dobj=F pass=F Number=Sing lemma=fi complet=F] npred:AdvP  
%reverse

#adverbial predicate
VP ::= AdvP[lemma=posibil,probabil,sigur,imposibil,evident,incontestabil,precis,destul,firește,negreșit, bineînțeles,desigur,pesemne]

CP ::= h:SConj VP[VerbForm=Fin]
REL ::= h:PRON[PronType==Rel]
REL ::= h:ADV[PronType==Rel]
REL ::= Prep[Case=@] h:REL[Case=@] # de când, în care, etc.
RP ::= h:REL VP



# Intrebări
QuesP ::= CCONJ RP # dar de unde știa el lucrul ăsta?

# și adverbial
PP ::= CCONJ[lemma=și] PP

# Conjunctii si locutiuni conjunctionale subordonatoare
SConj ::= h:SCONJ
SConj ::= h:ADV[lemma=ca]
SConj ::= h:ADV SCONJ	# cum că, chiar dacă, chiar de
SConj ::= h:ADP SCONJ # pentru că
# BARE_PP ::= h:ADP NOUN[Definite=Ind] # pe lângă
# BARE_PP ::= h:ADP PRON[PronType=Ind,Rel] # cu toate, după ce
# SConj ::= h:BARE_PP SCONJ # pe lângă că, cu toate că
# SConj ::= h:BARE_PP PRON[PronType=Rel] # pe măsură ce
# SConj ::= h:DET[lemma=cât CGN=@] NOUN[CGN=@ Case=Nom,Acc]  # câtă vreme, cât timp
# SConj ::= h:ADP PRON[lemma=ce] # după ce, până ce
# SConj ::= h:ADP[lemma=după] PRON[lemma=care] # după ce, până ce
# SConj ::= h:ADV PRON[lemma=ce] # îndată ce, odată ce, 
# SConj ::= h:ADP ADV[PronType=Rel lemma=cum,când,cât]	# după cum, pe când, după cât
# CA_SI ::= h:ADV[lemma=ca] CCONJ[lemma=și]
# SConj ::= h:CA_SI ADV[PronType=Rel lemma=când,cum] # ca și când, ca și cum
# nu stiu ce să fac cu: în concluzie, în consecință, ca atare

VPConj ::= CCONJ h:VP 
VP[complet=T coord=T] ::= h:VP VPConj

# parataxis is way at the end

AdjPConj ::=  CCONJ[lemma=și,sau,dar] h:AdjP  
AdjP[CGN=@] ::= h:AdjP[CGN=@] h:AdjPConj[CGN=@] 
AdjP[CGN=@] ::= h:AdjP[CGN=@] parataxis:AdjP[CGN=@] # Bucătăria era închisă, goală.

AdvPConj ::= CCONJ[lemma=și,sau,dar] h:AdvP
AdvP ::= h:AdvP AdvPConj 

PPConj ::= CCONJ[lemma=și,sau] PP  
PP ::= h:PP PPConj  

CPConj ::= CCONJ CP  
CP ::= h:CP CPConj  

DetPConj ::= CCONJ h:DetP  
DetP ::= h:DetP DetPConj  # patru sau mai multe

NPConj ::=  CCONJ[lemma=și,sau] h:NP # durata și
NP[Number=Plur Case=@] ::= h:NP[Case=@] NPConj[Case=@]  # durata și mortalitatea

VP[complet=T VerbForm=@ Mood=@] ::= h:VP[VerbForm=@ Mood=@] parataxis:VP[VerbForm=@ Mood=@]
VP ::= h:VP parataxis:VP[Mood==Imp] # merg, crede-ma, cat pot de repede



# punctuation

PUNCT ::= COMMA
PUNCT ::= PERIOD

NP ::= h:NP PUNCT
VP ::= h:VP PUNCT
PP ::= h:PP PUNCT
CP ::= h:CP PUNCT
RP ::= h:RP PUNCT
AdjP ::= h:AdjP PUNCT
AdvP ::= h:AdvP PUNCT